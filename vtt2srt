#!/usr/bin/perl

use utf8;
use strict;
use open IO  => ":encoding(utf8)";

our $usage = << 'EOU';
VTT to SRT subtitle file convertor.

Usage:
  vtt2srt some.vtt > some.srt 
  vtt2srt < some.vtt > some.srt 

EOU

print STDERR $usage and exit if $ARGV[0] =~ /^-/;

my $count = 1;
my $got_some_lines = 0;
my $buffer = '';

sub releave_and_restart {
	if ($got_some_lines) {
		print "$buffer\n";
		$count++;
	}
	$buffer = shift;
	$got_some_lines = 0;
}

while (<>) {
	chomp;

	if (/^(?:(\d+):)?(\d+):(\d+)\.(\d+)\s+-->\s+(?:(\d+):)?(\d+):(\d+)\.(\d+)\s*$/) {
		releave_and_restart(sprintf "%i\n%02i:%02i:%02i,%03i --> %02i:%02i:%02i,%03i\n", $count, $1, $2, $3, $4, $5, $6, $7, $8);
	} else {
		next unless $buffer && /^\s*(.+?)\s*$/;
		$buffer .= "$1\n";
		$got_some_lines = 1;
	}
}

&releave_and_restart;
